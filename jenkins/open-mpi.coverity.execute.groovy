// -*- groovy -*-
//
// Build an Open MPI dist release
//
//
// WORKSPACE Layout:
//   ompi-scripts/         ompi-scripts master checkout
//   coverity-tool/        Coverity tool build

def snapshot_version = ""
def ompi_tarball_name = ""
def ompi_dir = ""
def ompi_version = ""
def coverity_path = ""

currentBuild.displayName = "#${currentBuild.number}"
currentBuild.description = "Coverity Nightly Build for Open MPI\n"

node("ubuntu_26.04-x86_64") {
    stage('Tools Checkout') {
        checkout(changelog: false, poll: false, scm: scm)
    }

    stage('Fetch Coverity Tool') {
        sh("mkdir -p ${WORKSPACE}/coverity-tool")
        s3Download(file:'coverity-tool/coverity_tools.tgz', bucket:'ompi-jenkins-config',
                   path: 'coverity/coverity_tools.tgz', force: true)
        sh('cd coverity-tool ; tar xf coverity_tools.tgz')
        def cov_bin
        cov_bin = sh(script: "find ${WORKSPACE}/coverity-tool -name \"cov-build\" -print",
                     returnStdout: true).trim()
        coverity_path = sh(script: "dirname ${cov_bin}", returnStdout: true).trim()
        echo "path: ${coverity_path}"
    }

    stage('Fetch Open MPI') {
        sh("curl --fail -O https://download.open-mpi.org/nightly/open-mpi/main/latest_snapshot.txt")
        snapshot_version = sh(script: "cat latest_snapshot.txt", returnStdout: true).trim()

        currentBuild.displayName = "${currentBuild.displayName} - ${snapshot_version}"
        currentBuild.description = "${currentBuild.description} for version ${snapshot_version}"

        ompi_tarball_name = "openmpi-${snapshot_version}.tar.gz"
        sh("curl --fail -O https://download.open-mpi.org/nightly/open-mpi/main/${ompi_tarball_name}")
        sh("tar xf ${ompi_tarball_name}")

        def matcher = ("${ompi_tarball_name}" =~ /^openmpi-(.*)\.tar\..*/)
        if (matcher) {
            ompi_version = matcher[0][1]
	    ompi_dir="openmpi-${ompi_version}"
            echo "ompi_dir: ${ompi_dir}"
        } else{
            error "Cannot find ompi version and directory from ${ompi_tarball_name}"
        }
    }

    stage('Configure Open MPI') {
        sh("cd ${WORKSPACE}/${ompi_dir} && ./configure --enable-debug --enable-mpi-fortran --enable-mpi-java --enable-oshmem --enable-oshmem-fortran --with-usnic")
    }

    stage('Building Open MPI') {
        withEnv(["PATH+EXTRA=${coverity_path}"]) {
            sh("cd ${WORKSPACE}/${ompi_dir} && cov-build --dir cov-int make")
        }
    }

    stage('Submit Results') {
        sh("tar jcf ${WORKSPACE}/submission.tar.bz2 cov-int")
        withCredentials([usernamePassword(credentialsId: 'b47cf375-6e78-4f1f-b215-18a7903a4763',
                                          passwordVariable: 'token',
                                          usernameVariable: 'project')]) {
            sh("curl --form token=\"$token\" --form email=\"jsquyres@cisco.com\" --form file=@${WORKSPACE}/submission.tar.bz2 --form version=\"${ompi_version}\"  --form description=\"nightly-master\" \"https://scan.coverity.com/builds?project=$project\"")
	}
    }

    stage('Cleanup') {
        sh("rm -rf ${WORKSPACE}/*")
    }
}
